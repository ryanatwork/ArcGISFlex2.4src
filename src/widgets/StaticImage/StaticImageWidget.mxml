<?xml version="1.0" encoding="utf-8"?>
<!--
     ////////////////////////////////////////////////////////////////////////////////
     //
     // Copyright (c) 2010-2011 Esri
     //
     // All rights reserved under the copyright laws of the United States.
     // You may freely redistribute and use this software, with or
     // without modification, provided you include the original copyright
     // and use restrictions.  See use restrictions in the file:
     // <install location>/License.txt
     //
     ////////////////////////////////////////////////////////////////////////////////
-->
<viewer:BaseWidget xmlns:fx="http://ns.adobe.com/mxml/2009"
                   xmlns:s="library://ns.adobe.com/flex/spark"
                   xmlns:mx="library://ns.adobe.com/flex/mx"
                   xmlns:viewer="com.esri.viewer.*"
                   initialize="basewidget_initializeHandler(event)"
                   widgetConfigLoaded="init()">

    <fx:Script>
        <![CDATA[
            import com.esri.viewer.AppEvent;
            import com.esri.viewer.ViewerContainer;

            import flash.net.navigateToURL;

            import mx.events.FlexEvent;

            //This is the key used by this widget to get image url from published data.
            private const IMAGE_URL:String = "imageurl"

            private var linkURL:String;

            [Bindable]
            private var imageToolTip:String;

            private function init():void
            {
                if (configXML)
                {
                    var imgURL:String = configXML.imageurl;
                    img.source = imgURL;

                    linkURL = configXML.linkurl;
                    if (linkURL)
                    {
                        img.buttonMode = true;
                        img.useHandCursor = true;
                        img.addEventListener(MouseEvent.CLICK, img_clickHandler);
                    }

                    // tooltip
                    imageToolTip = configXML.tooltip;
                }
            }

            private function basewidget_initializeHandler(event:FlexEvent):void
            {
                // Allows other widgets to announce that image URL has been updated.
                AppEvent.addListener(AppEvent.DATA_PUBLISH, sharedDataUpdated);
            }

            private function sharedDataUpdated(event:AppEvent):void
            {
                var dataTable:Object = event.data;

                if (dataTable.key == IMAGE_URL)
                {
                    img.source = dataTable.collection[0];
                }
            }

            private function img_clickHandler(event:MouseEvent):void
            {
                navigateToURL(new URLRequest(linkURL));
            }
        ]]>
    </fx:Script>

    <mx:Image id="img" toolTip="{imageToolTip}"/>

</viewer:BaseWidget>
