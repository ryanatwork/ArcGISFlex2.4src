<?xml version="1.0" encoding="utf-8"?>
<!--
     ////////////////////////////////////////////////////////////////////////////////
     //
     // Copyright (c) 2010-2011 Esri
     //
     // All rights reserved under the copyright laws of the United States.
     // You may freely redistribute and use this software, with or
     // without modification, provided you include the original copyright
     // and use restrictions.  See use restrictions in the file:
     // <install location>/License.txt
     //
     ////////////////////////////////////////////////////////////////////////////////
-->
<s:Label xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:mx="library://ns.adobe.com/flex/mx"
         initialize="label1_initializeHandler(event)"
         lineBreak="explicit">
    <fx:Script>
        <![CDATA[
            import mx.core.EventPriority;
            import mx.core.FlexGlobals;
            import mx.core.UITextFormat;
            import mx.events.FlexEvent;

            /**
             * The minimal font size at which auto-shrinking stops.
             */
            public var minFontSize:Number = 10;

            public var autoShrinking:Boolean = true;

            private var _initialFontSize:Number;

            private var _initDone:Boolean = false;

            protected function label1_initializeHandler(event:FlexEvent):void
            {
                addEventListener(FlexEvent.UPDATE_COMPLETE, onUpdateComplete, false, EventPriority.DEFAULT_HANDLER);
            }

            protected function onUpdateComplete(event:FlexEvent):void
            {
                if (!_initDone)
                {
                    initFontSize();
                    _initDone = true;
                }

                width = maxWidth;
                constrainTextToWidth();

                if (!canFit())
                {
                    setStyle("lineBreak", "toFit");
                    maxDisplayedLines = 1;
                    showTruncationTip = true;
                }
                else
                {
                    setStyle("lineBreak", "explicit");
                    showTruncationTip = false;
                    this.width = Math.min(maxWidth, width + 5);
                }

                invalidateSize();
                validateNow();
            }

            private function constrainTextToWidth():void
            {
                if (width <= 0)
                {
                    return;
                }

                var topLevelStyleManager:IStyleManager2 = FlexGlobals.topLevelApplication.styleManager;

                var style:CSSStyleDeclaration = topLevelStyleManager.getStyleDeclaration("." + styleName);

                var fontSize:Number = _initialFontSize;
                setStyle("fontSize", fontSize);
                invalidateSize();
                validateNow();

                if (!autoShrinking)
                {
                    return;
                }

                while (fontSize > minFontSize && getTextWidth(text, fontSize, style) > width)
                {
                    fontSize = fontSize - 0.5;
                    setStyle("fontSize", fontSize);
                }
            }

            private function getTextWidth(text:String, fontSize:Number, style:CSSStyleDeclaration):Number
            {
                var textFormat:UITextFormat =
                    new UITextFormat(
                    FlexGlobals.topLevelApplication.systemManager,
                    style.getStyle("fontFamily"),
                    fontSize,
                    null,
                    style.getStyle("fontWeight") == "bold",
                    style.getStyle("fontStyle") == "italic",
                    null,
                    null,
                    null,
                    null,
                    style.getStyle("paddingLeft"),
                    style.getStyle("paddingRight"),
                    style.getStyle("textIndent"));
                textFormat.antiAliasType = flash.text.AntiAliasType.ADVANCED;
                textFormat.gridFitType = flash.text.GridFitType.PIXEL;
                var textMetrics:TextLineMetrics = textFormat.measureText(text);
                return textMetrics.width;
            }

            private function canFit():Boolean
            {
                var topLevelStyleManager:IStyleManager2 = FlexGlobals.topLevelApplication.styleManager;
                var style:CSSStyleDeclaration = topLevelStyleManager.getStyleDeclaration("." + styleName);
                var fontSize:Number = style.getStyle("fontSize") as Number;

                return getTextWidth(text, fontSize, style) <= width;
            }

            private function initFontSize():void
            {
                var topLevelStyleManager:IStyleManager2 = FlexGlobals.topLevelApplication.styleManager;
                var style:CSSStyleDeclaration = topLevelStyleManager.getStyleDeclaration("." + styleName);

                _initialFontSize = style.getStyle("fontSize") as Number;
            }
        ]]>
    </fx:Script>
</s:Label>
